<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="SpatialIndex.SQLResult">
<Super>%SQL.AbstractFind</Super>
<TimeCreated>63812,80595.195041</TimeCreated>

<Property name="ResultBits">
<MultiDimensional>1</MultiDimensional>
<Private>1</Private>
</Property>

<Method name="%OnNew">
<Private>1</Private>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	kill i%ResultBits
	kill qHandle
	quit $$$OK
]]></Implementation>
</Method>

<Method name="PrepareFind">
<FormalSpec>indexGlobal:%String,queryType:%String,queryParams:%Binary</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if queryType = "window" {
		for i = 1:1:4 {
			set item = $Piece(queryParams, ",", i)
			set param = $Piece(item, "=", 1)
			set value = $Piece(item, "=" ,2)
			set arg(param) = value
		}
		//set rset = ##class(%ResultSet).%New("SpatialIndex.QueryExecutor:FindWindow")
	 	//do rset.Execute(indexGlobal,arg("minx"),arg("miny"),arg("maxx"),arg("maxy"))
	 	//while (rset.Next()){
		 //	set id = rset.Get("Id")
	 	set qHandle("indexGlobal") = indexGlobal
	 	do ##class(SpatialIndex.QueryExecutor).InternalFindWindow(.qHandle,arg("minx"),arg("miny"),arg("maxx"),arg("maxy"))
	 	set id = ""
	 	for  {
		 	set id = $O(qHandle("data", id),1,idd)
		 	quit:id=""
	 		set tChunk = (idd\64000)+1, tPos=(idd#64000)+1
	 		set $BIT(i%ResultBits(tChunk),tPos) = 1
	 	}
	}elseif queryType = "radius" {
		/*
		SELECT *
		FROM SpatialIndex.Test
		WHERE  %ID %FIND search_index(x1F,'radius','x=55,y=55,radius=100m')
  		and name %StartsWith 'Z'
  		*/
  		for i = 1:1:$L(queryParams, ",") {
			set item = $Piece(queryParams, ",", i)
			set param = $Piece(item, "=", 1)
			set value = $Piece(item, "=" ,2)
			set arg(param) = value
		}
		set qHandle("indexGlobal") = indexGlobal
		set:'$D(arg("radiusX")) arg("radiusX") = arg("radius")
		set:'$D(arg("radiusY")) arg("radiusY") = arg("radius")
	 	do ##class(SpatialIndex.QueryExecutor).InternalFindRadius(.qHandle,arg("x"),arg("y"),arg("radiusX"),arg("radiusY"))
	 	set id = ""
	 	for  {
		 	set id = $O(qHandle("data", id),1,idd)
		 	quit:id=""
	 		set tChunk = (idd\64000)+1, tPos=(idd#64000)+1
	 		set $BIT(i%ResultBits(tChunk),tPos) = 1
	 	}
	}
	quit $$$OK
]]></Implementation>
</Method>

<Method name="ContainsItem">
<FormalSpec>pItem:%String</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	set tChunk = (pItem\64000)+1, tPos=(pItem#64000)+1
	quit $bit($get(i%ResultBits(tChunk)),tPos)
]]></Implementation>
</Method>

<Method name="GetChunk">
<FormalSpec>pChunk:%Integer</FormalSpec>
<ReturnType>%Binary</ReturnType>
<Implementation><![CDATA[	quit $get(i%ResultBits(pChunk))
]]></Implementation>
</Method>

<Method name="NextChunk">
<FormalSpec><![CDATA[&pChunk:%Integer=""]]></FormalSpec>
<ReturnType>%Binary</ReturnType>
<Implementation><![CDATA[
	set pChunk = $order(i%ResultBits(pChunk),1,tBits)
	quit:pChunk="" ""
	quit tBits
]]></Implementation>
</Method>

<Method name="PreviousChunk">
<FormalSpec><![CDATA[&pChunk:%Integer=""]]></FormalSpec>
<ReturnType>%Binary</ReturnType>
<Implementation><![CDATA[
	set pChunk = $order(i%ResultBits(pChunk),-1,tBits)
	quit:pChunk="" ""
	quit tBits
]]></Implementation>
</Method>
</Class>
</Export>
